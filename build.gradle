apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile 'org.assertj:assertj-core:3.1.0'
}

sourceSets {
    integration {
        java.srcDir file('src/integration/java')
        resources.srcDir file('src/integration/resources')
    }
    
    dependencies {
        // ...
        integrationCompile sourceSets.main.output
        integrationCompile configurations.testCompile
        integrationCompile sourceSets.test.output
        integrationRuntime configurations.testRuntime
        // ...
    }
}

task integration(type: Test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/integration.exec")
    }
}

test {
    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/unit.exec")
    }
}


jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task mergecov(type: JacocoMerge) {
    // can pass in any task set up for Jacoco
    executionData test, integration
    destinationFile = new File("$buildDir/jacoco/test.exec")
}

check.dependsOn(test, integration, jacocoTestReport)    
