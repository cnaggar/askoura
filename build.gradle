apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'jetty'

mainClassName = "com.askoura.library.Launcher"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.eclipse.jetty:jetty-webapp:9.3.2.v20150730'
    compile 'org.hibernate:hibernate-entitymanager:4.3.11.Final'

    compile 'org.springframework:spring-webmvc:4.2.0.RELEASE'
    compile 'org.springframework:spring-jdbc:4.2.0.RELEASE'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'

    compile 'commons-collections:commons-collections:3.2'
    compile 'commons-lang:commons-lang:2.6'

    compile 'com.h2database:h2:1.4.188'
    compile 'com.zaxxer:HikariCP:2.4.1'

    // test
    testCompile 'junit:junit:4.+'
    testCompile 'org.assertj:assertj-core:3.1.0'
    testCompile 'com.jayway.restassured:rest-assured:2.5.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.springframework:spring-test:4.2.0.RELEASE'

}

sourceSets {
    integration {
        java.srcDir file('src/integration/java')
        resources.srcDir file('src/integration/resources')
    }

    dependencies {
        // ...
        integrationCompile sourceSets.main.output
        integrationCompile configurations.testCompile
        integrationCompile sourceSets.test.output
        integrationRuntime configurations.testRuntime
        // ...
    }
}


task integration(type: Test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

test {
    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}


jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task mergecov(type: JacocoMerge) {
    // can pass in any task set up for Jacoco
    executionData test, integration
    destinationFile = new File("$buildDir/jacoco/test.exec")
}

check.dependsOn(test, integration, jacocoTestReport)    
